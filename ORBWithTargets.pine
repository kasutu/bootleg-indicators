//@version=6
indicator("ORB + Targets [kasutufx]", overlay=true)

// Inputs
inputMax = input(5, title="ORB total time (minutes)")
sess = input.session("0915-0920", title="Session Time") 
volConfirm = input.bool(true, title="Require Volume Confirmation")
volMultiplier = input.float(1.5, title="Volume Multiplier", minval=1.0, maxval=5.0)
dark_mode = input.bool(true, "Dark Mode", tooltip="Toggle between light and dark monochrome theme")

// Color scheme
line_color = dark_mode ? color.white : color.black
label_bg_color = dark_mode ? color.black : color.white
label_text_color = dark_mode ? color.white : color.black

// Session logic
t = time(timeframe.period, sess + ":1234567")
hide = timeframe.isintraday and timeframe.multiplier <= inputMax
in_session = not na(t)
is_first = in_session and not in_session[1]

// ORB levels
orb_high = float(na)
orb_low = float(na)

if is_first
    orb_high := high
    orb_low := low
else
    orb_high := orb_high[1]
    orb_low := orb_low[1]
if high > orb_high and in_session
    orb_high := high
if low < orb_low and in_session
    orb_low := low

// Target levels
orb_range = orb_high - orb_low
target_levels = array.from(orb_high + orb_range * 2, orb_high + orb_range * 4, orb_high + orb_range * 6, 
                          orb_low - orb_range * 2, orb_low - orb_range * 4, orb_low - orb_range * 6)

// Breakout confirmation
avgVolume = ta.sma(volume, 20)
volumeConfirmed = not volConfirm or (volume > avgVolume * volMultiplier)
bullish_breakout = close > orb_high and close[1] <= orb_high and volumeConfirmed and not in_session
bearish_breakout = close < orb_low and close[1] >= orb_low and volumeConfirmed and not in_session

// Track confirmed breakouts
var bool confirmed_bullish = false
var bool confirmed_bearish = false
var int session_start_bar = na

if bullish_breakout
    confirmed_bullish := true
    confirmed_bearish := false
if bearish_breakout
    confirmed_bearish := true
    confirmed_bullish := false
if is_first
    confirmed_bullish := false
    confirmed_bearish := false
    session_start_bar := bar_index

// Helper function to draw lines and labels
draw_level(start_bar, level, text_label, line_width = 1) =>
    if barstate.islast and hide and not na(level) and not na(start_bar)
        line.new(x1=start_bar, y1=level, x2=bar_index, y2=level, color=line_color, width=line_width)
        label.new(x=bar_index, y=level, text=text_label, style=label.style_label_left, 
                 color=label_bg_color, textcolor=label_text_color, size=size.small)

// Draw ORB levels
draw_level(session_start_bar, orb_high, "Entry", 2)
draw_level(session_start_bar, orb_low, "SL", 2)

// Draw targets after confirmed breakout
if confirmed_bullish
    for i = 0 to 2
        draw_level(session_start_bar, array.get(target_levels, i), "TP" + str.tostring(i + 1))

if confirmed_bearish
    for i = 3 to 5
        draw_level(session_start_bar, array.get(target_levels, i), "TP" + str.tostring(i - 2))

// Alert conditions
alertcondition(bullish_breakout, title="ORB Bullish Breakout Confirmed", message="ORB bullish breakout confirmed with volume")
alertcondition(bearish_breakout, title="ORB Bearish Breakout Confirmed", message="ORB bearish breakout confirmed with volume")